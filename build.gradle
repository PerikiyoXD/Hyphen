plugins {
    id 'java-library'

    // Publishing
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group 'dev.quantumfusion'
version '0.2.1'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation libs.asm
    implementation libs.asm.util
    implementation libs.jetbrains.annotations
    
    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'utf-8'
}

java {
    withSourcesJar()
    withJavadocJar()
}

// Publishing

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}
publishing {
    repositories {
        maven {
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
    publications {
        hyphen(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = 'Serialize at the speed of light.'
                url = 'https://github.com/TeamQuantumFusion/Hyphen'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TeamQuantumFusion/Hyphen.git'
                    developerConnection = 'scm:git:ssh://github.com:TeamQuantumFusion/Hyphen.git'
                    url = 'http://github.com/TeamQuantumFusion/Hyphen/tree/rework-9'
                }
                developers {
                    developer {
                        id = 'notalpha'
                        name = 'Yan Chubikov'
                        email = 'yan.gyunashyan@gmail.com'
                    }
                }
            }
        }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    setRequired {
        gradle.taskGraph.allTasks.any { 
            it.equals(PublishToMavenRepository) 
        }
    }
    sign publishing.publications.hyphen
    sign configurations.archives
}
